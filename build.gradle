buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.liquibase:liquibase-gradle-plugin:1.2.4')
        classpath('mysql:mysql-connector-java:5.1.13')
    }
}

apply plugin: 'org.liquibase.gradle'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'com.qaiware.chat'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-log4j2')
    compile('org.springframework.boot:spring-boot-starter-data-rest') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    compile('org.springframework.boot:spring-boot-starter-data-jpa') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    compile('org.hibernate:hibernate-java8:5.0.12.Final')
    compile('io.springfox:springfox-swagger-ui:2.7.0')
    compile('io.springfox:springfox-swagger2:2.7.0')
    compile('org.projectlombok:lombok:1.16.18')
    compile(group: 'net.sf.dozer', name: 'dozer', version: '5.4.0') {
        exclude(module: 'slf4j-log4j12')
        exclude(module: 'slf4j-api')
    }
    compile('mysql:mysql-connector-java:6.0.6')
    compile group: 'com.google.code.gson', name: 'gson', version:'2.7'
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile 'com.h2database:h2:1.4.194'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version:'1.0.6'
}

def changeLog = "${projectDir}/src/main/resources/master.xml"
def liquibaseProp = "${project.rootDir}/src/main/resources/application-dev.properties"

liquibase {
    activities {
        main {
            File propsFile = new File(liquibaseProp)
            Properties properties = new Properties()
            properties.load(new FileInputStream(propsFile))
            changeLogFile changeLog
            driver properties['spring.datasource.driver-class-name']
            url properties['spring.datasource.url']
            username properties['spring.datasource.username']
            password properties['spring.datasource.password']
        }
    }
    runList = "main"
}
build.dependsOn update
